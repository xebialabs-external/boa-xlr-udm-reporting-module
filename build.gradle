buildscript {
    repositories {
        mavenLocal()
        ["public", "thirdparty", "releases"].each { r ->
            maven {
                credentials {
                    username nexusUserName
                    password nexusPassword
                }
                url "${nexusBaseUrl}/repositories/${r}"
            }
        }
    }

    dependencies {
        classpath "com.xebialabs.gradle.plugins.xlrelease:gradle-xl-release-plugin-plugin:0.1.2"
        classpath "com.moowork.gradle:gradle-node-plugin:1.1.1"
    }
}

plugins {
    // id "com.github.hierynomus.license" version "0.14.0"
    id "com.xebialabs.xl.docker" version "1.1.0"
}

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'idea'

apply plugin: 'com.xebialabs.xl-release.plugin'
apply plugin: 'xebialabs.root.opinions'
apply plugin: 'xebialabs.opinions'
apply plugin: 'com.moowork.node'

group 'com.xebialabs.xlrelease.plugins'
version = '0.2.0'
def xlreleaseHome = "$System.env.XLRELEASE_HOME"

xlDocker {
    compileImage = 'xebialabs/xlr_dev_compile'
    compileVersion = 'v7.0.1.1'
    runImage = 'xebialabs/xlr_dev_run'
    runVersion = 'v7.0.1.1'
    runPortMapping = '15516:5516'
}

dependencies {
    compile fileTree(dir: "$xlreleaseHome/lib", include: ['*.jar'])
    compile 'org.scala-lang:scala-library:2.12.8'
}

node {
    version = '8.9.0'
    yarnVersion = '0.22.0'
    npmVersion = '3.10.9'
    download = true
    nodeModulesDir = file("${rootProject.projectDir}")
}

sourceSets {
    // js are processed by webpack
    main {
        resources {
            exclude '**/*.js'
        }
    }
}

task yarnRunBuild(type: YarnTask) {
    args = ['run', 'build']
}

yarnRunBuild.dependsOn yarn_install
processResources.dependsOn yarnRunBuild
